---
title: Servidor de Factorio declarativo com NixOS+Terraform no Magalu Cloud
description: Aprenda como usar Terraform e NixOS para facilmente subir VMs com suas aplica√ß√µes no Magalu Cloud.
language: pt
---

> **This blog post was originally published on [dev.to](https://dev.to/magalucloud/servidor-de-factorio-100-declarativo-com-nixos-e-terraform-no-magalu-cloud-3f37) as a guide for Magalu Cloud (my employer) hackathon's participants.**

Uma d√∫vida comum que pessoas novas ao Terraform t√™m √©: ‚Äúdepois de criar a VM, como eu rodo minha aplica√ß√£o?‚Äù. Algumas solu√ß√µes incluem cloud-init, Ansible, etc.

Nesse guia, eu quero mostrar uma das melhores (na minha opini√£o) solu√ß√µes para isso: o NixOS. Mostrarei como usar Terraform + NixOS para provisionar um servidor j√° rodando uma aplica√ß√£o de sua escolha, sem nenhum passo manual.

Factorio √© um jogo de automa√ß√£o com uma qualidade absurda e uma gameplay extremamente polida. Se voc√™ valoriza seu sono, recomendo n√£o jogar! Pro resto de n√≥s, j√° viciados, a f√°brica deve crescer!

Com isso em mente, esse guia ir√°, como exemplo divertido, focar em subir um servidor de Factorio! ‚öôÔ∏è

> *Mas, Gabriel, eu n√£o gosto de Factorio!*
>
> √ìtimo! Melhor ficar longe desse v√≠cio.
>
> O factorio √© s√≥ um exemplo, e √© a parte menos importante do guia. Explicarei como fazer isso para outros jogos (e.g. minecraft), continue lendo!

O vers√£o final est√° dispon√≠vel aqui: [GitHub - Misterio77/hackathon-mgc-factorio-terraform](https://github.com/misterio77/hackathon-mgc-factorio-terraform). Esse tutorial ir√° mostrar como construir esse c√≥digo iterativamente.

## Intro üèÅ

O NixOS √© uma distribui√ß√£o Linux baseada no gerenciador de pacotes Nix. O Nix permite empacotar programas de forma reproduz√≠vel e isolada, numa linguagem declarativa e pura. O NixOS leva isso a um outro n√≠vel, e permite configurar sistemas inteiros usando essa mesma linguagem. Por exemplo, para subir um servidor de Factorio:

```nix
{
  services.factorio = {
    enable = true;
  };
}
```

Lembra bastante o Terraform, n√©?

Vou mostrar pra voc√™s como implantar e configurar um servidor no Magalu Cloud, via Terraform e NixOS, por meio do [nixos-anywhere](https://github.com/nix-community/nixos-anywhere).

A id√©ia √© que, com apenas um `tofu apply`, o servidor seja criado j√° rodando exatamente o que voc√™ quer que rode, sem nenhum passo manual. :construction_worker:

M√£os √† obra!

## Setup üî®

Caso queira acompanhar o tutorial e ir rodando coisas na sua m√°quina (que recomendo!), voc√™ vai precisar:

* Qualquer distro Linux (pode ser WSL) ou MacOS;
* Instalar o gerenciador de pacotes [Nix](https://nixos.org/download) (sua m√°quina **n√£o** precisa ser NixOS);
* Instalar a [MGC CLI](https://docs.magalu.cloud/docs/devops-tools/cli-mgc/overview/);
* Instalar o [OpenTofu](https://opentofu.org/docs/intro/install/);
* Uma chave SSH;
* Um editor de texto que voc√™ goste.

## Configura√ß√£o de NixOS ‚ùÑÔ∏è

Para usarmos algumas funcionalidades novas do Nix, vamos habilitar `flakes` e `nix-command`:

```shell
$ mkdir -p ~/.config/nix
$ echo "extra-experimental-features = nix-command flakes" >> ~/.config/nix/nix.conf
```

Vamos come√ßar configurando nosso NixOS. Crie uma `configuration.nix`:

```nix
{
  networking = {
    hostName = "factorio-server";
    # Usar DHCP para conectar
    useDHCP = true;
  };

  system.stateVersion = "25.05";
  nixpkgs = {
    # Arquitetura e sistema
    hostPlatform = "x86_64-linux";
    # Habilitar pacotes propriet√°rios
    config.allowUnfree = true;
  };

  services.factorio = {
    enable = true;
    # Abrir porta no firewall
    openFirewall = true;
  };

  # TODO: Iremos remover isso depois
  users.users.root = {
    initialPassword = "123456";
  };
}
```

> *Gabriel, o que mais podemos rodar al√©m de factorio?*
>
> O que seu cora√ß√£o mandar! O NixOS tem [abstra√ß√µes prontas](https://search.nixos.org/options) para v√°rios jogos, e voc√™ pode rodar qualquer software que imaginar.
> Experimente `services.minecraft`!

Vamos usar flakes nesse tutorial. Crie uma `flake.nix`:

```nix
{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
  };

  outputs = { nixpkgs, ... }: {
    nixosConfigurations.factorio-server = nixpkgs.lib.nixosSystem {
      modules = [./configuration.nix];
    };
  };
}
```

> *Gabriellll, o que √© diachos √© um flake?*
>
> √ìtima pergunta! Ter um `flake.nix` torna seu reposit√≥rio um flake (duh). Em resumo, um flake √© um "projeto nix", com depend√™ncias em outros "projetos nix", e que prov√™ "coisas nix". Nesse caso, nosso projeto depende do [`nixpkgs`](https://github.com/nixos/nixpkgs) (o conjunto principal de software empacotado para Nix) na vers√£o `nixos-unstable`, e prov√™ uma configura√ßao de NixOS chamada `factorio-server`.
>
> √â poss√≠vel usar Nix sem flakes, mas criar um flake te permite gerir melhor as depend√™ncias do seu projeto e usar uma CLI mais intuitiva.


Vamos testar? O Nix permite criar uma VM para uma dada configura√ß√£o de NixOS. Rode:

```shell
$ nix run .#nixosConfigurations.factorio-server.config.system.build.vm
```

Pode levar alguns minutos, pois o Nix ir√° baixar absolutamente tudo nescess√°rio para esse sistema. Fique tranquilo, pois seu sistema ir√° re-usar isso sempre que poss√≠vel.

Ir√° abrir uma janela com o console da sua VM. Fa√ßa login com `root` e `123456`.

![Uma captura de tela mostrando um console QEMU, com login no tty feito no usu√°rio root](https://canada1.discourse-cdn.com/flex008/uploads/jaxy/original/1X/aa442cc9cf735b530098e1b800e9e6d514f1c61f.png?v=1)

Vamos ver se o servidor est√° okay:

```shell
systemctl status factorio
```

![Uma captura de tela mostrando um console QEMU, ap√≥s executar 'systemctl status factorio', mostrando que ele est√° executando corretamente](https://canada1.discourse-cdn.com/flex008/uploads/jaxy/original/1X/7698218f20b9ca2bd9062715a1968779a0132fc0.png?v=1)

Sucesso! Nossa VM est√° rodando Factorio ‚öôÔ∏è

Agora, vamos colocar isso na Cloud! Bora para o Terraform. Depois voltaremos para fazer alguns ajustes nessa configura√ß√£o.

## Subindo VM com Terraform ‚òÅÔ∏è

Crie um arquivo `main.tf`, com o conte√∫do:

```tf
terraform {
  backend "local" {
    path = ".terraform.tfstate"
  }
  required_providers {
    mgc = {
      source = "registry.terraform.io/magalucloud/mgc"
      version = "0.36.1"
    }
  }
}

provider "mgc" {
  region = "br-se1"
}
```

Nice. Esse √© o nosso boilerplate b√°sico pra usar o provider e manter o estado do terraform no nosso diret√≥rio local.

O terraform l√™ todas as `.tf` no diret√≥rio, ent√£o vamos deixar as coisas organizadinhas.

### Criando a VM

Vamos preparar nossa VM. Crie um `vm.tf`:

```tf
resource "mgc_virtual_machine_instances" "factorio_server" {
  name = "factorio"
  # 2 vCPUs, 8GB de RAM, 40GB de disco
  machine_type = "BV2-8-40"
  image = "cloud-debian-12 LTS"
}
```

> *U√©, Debian? N√£o √≠amos usar NixOS? Que sacril√©gio √© esse?*
>
> Calma calma foguetinho üöÄ! O Magalu Cloud ainda n√£o tem imagem de NixOS, mas temos uma carta na manga para instalar e configurar o NixOS, aguenta ai!

### Tornando ela acess√≠vel ao mundo

Precisamos acessar essa VM. Pra isso, vamos:
1. Adicionar nossa chave SSH
2. Alocar um IP p√∫blico e atribuir ele √† m√°quina
3. Abrir a porta do SSH (22 TCP) e do Factorio (34197 UDP) no firewall da cloud

Come√ßando pela chave p√∫blica. Na sua `vm.tf`:

```tf
resource "mgc_ssh_keys" "key" {
  name = "chave-do-gabriel"
  # Altere para a sua chave publica
  key = "<SUA CHAVE SSH PUBLICA>"
}

resource "mgc_virtual_machine_instances" "factorio_server" {
  name = "factorio"
  # 2 vCPUs, 8GB de RAM, 40GB de disco
  machine_type = "BV2-8-40"
  image = "cloud-debian-12 LTS"
  # Passar nossa chave
  ssh_key_name = mgc_ssh_keys.key.name
}
```

E vamos alocar um IPv4 dedicado e atribuir √† maquina, crie uma `ip.tf`:
```tf
# Alocar um IPv4
resource "mgc_network_public_ips" "factorio_ip" {
  vpc_id = mgc_virtual_machine_instances.factorio_server.vpc_id
}

# Atribuir √† m√°quina
resource "mgc_network_public_ips_attach" "factorio_ip_attach" {
  public_ip_id = mgc_network_public_ips.factorio_ip.id
  interface_id = mgc_virtual_machine_instances.factorio_server.network_interfaces[0].id
}

# O terraform vai mostrar esse valor para n√≥s
output "ip" {
  value = mgc_network_public_ips.factorio_ip.public_ip
}
```

Por fim, vamos abrir as portas. Crie uma `firewall.tf`:
```tf
# Nosso security group
resource "mgc_network_security_groups" "factorio_server" {
  name = "factorio-hackathon"
}

# Regra para SSH
resource "mgc_network_security_groups_rules" "incoming_ssh" {
  direction = "ingress"
  port_range_min = 22
  port_range_max = 22
  protocol = "tcp"
  ethertype = "IPv4"
  remote_ip_prefix = "0.0.0.0/0"
  security_group_id = mgc_network_security_groups.factorio_server.id
}
# Regra para o factorio
resource "mgc_network_security_groups_rules" "incoming_factorio" {
  direction = "ingress"
  port_range_min = 34197
  port_range_max = 34197
  protocol = "udp"
  ethertype = "IPv4"
  remote_ip_prefix = "0.0.0.0/0"
  security_group_id = mgc_network_security_groups.factorio_server.id
}

# Atribuir o group √† nossa VM
resource "mgc_network_security_groups_attach" "factorio_firewall_attach" {
  security_group_id = mgc_network_security_groups.factorio_server.id
  interface_id      = mgc_virtual_machine_instances.factorio_server.network_interfaces[0].id
}
```

> Lembre-se de trocar (ou adicionar) regras conforme o que voc√™ est√° deployando. Por exemplo, se est√° subindo minecraft, fa√ßa uma regra que abre a porta `25565` no protocolo `tcp`.

### O primeiro deploy

Maravilha! Hora de deployar. Vamos come√ßar autenticando via MGC CLI:

```shell
$ mgc auth login
```

E siga os passos na tela.

Agora precisamos criar uma API key e passar para o terraform.

```shell
$ mgc auth api-key create --name terraform-factorio
```

Aperte a flecha direita para dar todas as permiss√µes, e tab para aceitar. Agora vamos pegar a key e passar para o terraform. Um oneliner que facilita:

```shell
export TF_VAR_mgc_api_key=$(mgc auth api-key get --id $(mgc auth api-key list -o json -r | jq 'map(select(.name == "terraform-factorio"))[0].id' -r) -o json -r | jq .api_key -r)
```

Feito isso, vamos preparar o terraform:

```shell
$ tofu init
```

E aplicar:

```shell
$ tofu apply
```

Digite `yes`, e aguarde um pouquinho.

Feito isso, √© hora de validar que o servidor est√° okay e est√° acess√≠vel pela sua chave. O seu apply deve ter retornado o `ip` como output. Rode:

```shell
$ ssh debian@<IP DA VM>
```

Agora temos uma VM‚Ä¶ Rodando Debian (por enquanto):

![Uma captura de tela de um terminal conectado por SSH na VM do Magalu, rodando screenfetch e exibindo o logo do debian](https://canada1.discourse-cdn.com/flex008/uploads/jaxy/original/1X/6f6ec1c34d15f3c917db776ec3433b3ebc359852.png?v=1)

Vamos agora infectar essa querida com NixOS! Iremos utilizar o [nixos-anywhere](https://github.com/nix-community/nixos-anywhere).

## Instalando NixOS na VM ü™Ñ

A id√©ia do `nixos-anywhere` √© iniciar um NixOS via kexec, desmontar o disco da m√°quina, re-particionar ele, e instalar NixOS de verdade (com a nossa configura√ß√£o). Eles prov√™m um m√≥dulo de Terraform, que √© perfeito para a gente!

> *Gabriel, kexec? Que diacho √© isso?*
>
> N√£o tema! √â bem mais simples do que parece. Significa "**k**ernel **exec**ute".
>
> Pense assim: quando voc√™ vai instalar Linux numa m√°quina, voc√™ geralmente boota uma ISO por pendrive. Isso √© necess√°rio para voc√™ poder liberar e formatar o disco do seu computador.
>
> No Magalu n√£o temos como espetar um pendrive, ent√£o usamos um truque de executar outro Linux direto na mem√≥ria RAM da m√°quina, isso nos permite desmontar e formatar os discos da m√°quina, igual com um pendrive!

Vamos precisar fazer alguns ajustes na nossa configura√ß√£o de NixOS para comportar isso. O NixOS anywhere usa uma ferramenta chamada [disko](https://github.com/nix-community/disko) para particionar declarativamente. Vamos adicion√°-la no nosso `flake.nix`:

```nix
{
  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs/nixos-unstable";
    disko.url = "github:nix-community/disko/latest";
    disko.inputs.nixpkgs.follows = "nixpkgs";
  };

  outputs = { nixpkgs, disko, ... }: {
    nixosConfigurations.factorio-server = nixpkgs.lib.nixosSystem {
      modules = [
        ./configuration.nix
        disko.nixosModules.disko
      ];
    };
  };
}
```

Rode:

```shell
$ nix flake lock
```

Para registrar a mudan√ßa na flake.lock.

Nice. Agora vamos adicionar as configura√ß√µes espec√≠ficas da m√°quina (parti√ß√µes, m√≥dulos de kernel). Vamos fazer isso num arquivo separado da `configuration.nix`, para separar o ‚Äúwhat it runs‚Äù e o ‚Äúwhere it runs‚Äù.

Crie um arquivo `hardware-configuration.nix`:

```nix
{modulesPath, ...}: {
  imports = [(modulesPath + "/profiles/qemu-guest.nix")];
  boot = {
    initrd.availableKernelModules = ["ata_piix" "uhci_hcd"];
    kernelModules = ["kvm-intel"];
  };

  # Nossas parti√ß√µes
  disko.devices.disk.main = {
    device = "/dev/vda";
    type = "disk";
    content = {
      type = "gpt";
      partitions = {
        boot = {
          size = "1M";
          type = "EF02";
        };
        esp = {
          size = "512M";
          type = "EF00";
          content = {
            type = "filesystem";
            format = "vfat";
            mountpoint = "/boot";
          };
        };
        root = {
          size = "100%";
          content = {
            type = "filesystem";
            format = "ext4";
            mountpoint = "/";
          };
        };
      };
    };
  };
}
```

N√£o se preocupe muito se isso parece complexo. A maior parte desse arquivo foi gerado automaticamente. A parte do `disko` define quais nossas parti√ß√µes.

Precisamos importar esse arquivo da nossa `configuration.nix`. Tamb√©m vamos tirar a senha `123456`, e habilitar SSH. Edite ela:

```nix
{
  imports = [
    ./hardware-configuration.nix
  ];

  networking = {
    hostName = "factorio-server";
    useDHCP = true;
  };

  system.stateVersion = "25.05";
  nixpkgs = {
    hostPlatform = "x86_64-linux";
    config.allowUnfree = true;
  };

  services.factorio = {
    enable = true;
    openFirewall = true;
  };

  services.openssh = {
    enable = true;
    settings = {
      PermitRootLogin = "yes";
      PasswordAuthentication = false;
    };
  };

  users.users.root = {
    openssh.authorizedKeys.keys = [
      # Troque para sua chave.
      "<SUA CHAVE SSH PUBLICA>"
    ];
  };
}
```

Lembre-se de trocar a chave SSH para a sua.

> *Gabriel, por que temos a chave SSH em dois lugares?*
>
> A do terraform define qual vai ser a chave autorizada assim que a m√°quina √© provisionada. Essa chave ser√° usada pela instala√ß√£o inicial do nixos-anywhere. A do NixOS √© qual ser√° a chave autorizada ap√≥s a instala√ß√£o (e nescess√°ria para rebuilds).
> No reposit√≥rio (link no fim do post), fizemos de um jeito mais robusto: o terraform cria a chave.

Certo, agora vamos configurar o nixos-anywhere pelo terraform. Crie um `nixos.tf`:

```tf
module "deploy" {
  source = "github.com/nix-community/nixos-anywhere//terraform/all-in-one"
  nixos_system_attr = ".#nixosConfigurations.factorio-server.config.system.build.toplevel"
  nixos_partitioner_attr = ".#nixosConfigurations.factorio-server.config.system.build.diskoScript"
  debug_logging = true

  instance_id = mgc_virtual_machine_instances.factorio_server.id
  target_host = mgc_virtual_machine_instances.factorio_server.network.public_address
  install_user = "debian"
}
```

Adicionamos um novo m√≥dulo externo, ent√£o rode novamente o init:

```shell
$ tofu init
```

Certinho! Estamos prontos. Vamos aplicar a configura√ß√£o do Terraform:

```shell
$ tofu apply
```

Aguarde alguns minutos (geralmente menos de 5). O NixOS anywhere ir√° instalar NixOS na VM, e aplicar nossa configura√ß√£o!

Sempre que voc√™ modificar a configura√ß√£o, basta dar apply novamente, ele ir√° detectar a mudan√ßa e fazer altera√ß√µes na VM conforme nescess√°rio.

Sucesso! Podemos usar o IP agora para jogar factorio:

![Uma captura de tela nos menus da tela inicial do factorio, est√° sendo feita uma conex√£o direta ao IP da VM](https://canada1.discourse-cdn.com/flex008/uploads/jaxy/original/1X/135ef87fb35016ea28af6319e543245f91ae7389.jpeg)


## Fechamento

Espero que esse tutorial tenha ajudado voc√™ a ver algumas das coisas que s√£o poss√≠veis no modelo declarativo!

A magia da coisa √© que qualquer um pode rodar `tofu apply` e ter um servidor exatamente igual. Fazendo as mudan√ßas nos arquivos e rodando apply, voc√™ garante que n√£o existe nenhum passo de setup (e.g. instale coisa X, altere arquivo Y) al√©m de simplesmente ter os arquivos `.tf` e `.nix`.

O vers√£o final est√° dispon√≠vel no reposit√≥rio: [GitHub - Misterio77/hackathon-mgc-factorio-terraform](https://github.com/misterio77/hackathon-mgc-factorio-terraform)

Feedback √© muito bem vindo, e fico a disposi√ß√£o para qualquer d√∫vida!

Beijos,
Gab

